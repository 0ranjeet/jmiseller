apply plugin: 'com.android.application'

android {
    namespace "com.jmi.jmiseller"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.jmi.jmiseller"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def appName = "JMISeller"
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            def date = new Date()
            def formattedDate = date.format('yyyyMMdd')
            
            if (output.getFilter(com.android.build.OutputFile.ABI)) {
                // For APK splits
                outputFileName = "${appName}-v${versionName}(${versionCode})_${formattedDate}_${output.getFilter(com.android.build.OutputFile.ABI)}.apk"
            } else {
                // For APK or AAB
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.aab')) {
                    // For AAB files
                    outputFileName = "${appName}-v${versionName}(${versionCode})_${formattedDate}.aab"
                } else if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    // For APK files
                    outputFileName = "${appName}-v${versionName}(${versionCode})_${formattedDate}.apk"
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
